buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
  }
}

allprojects {
  apply plugin: "eclipse"
  apply plugin: "idea"

  group = "com.linkedin.avro2tf"
  project.version = "0.1.5"
  repositories {
    jcenter()
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
}

subprojects {
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'maven-publish'
  apply plugin: 'java'

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  // add javadoc/source jar tasks as artifacts
  artifacts {
    archives sourcesJar, javadocJar
  }

  publishing {
    publications {
      avro2tfPublication(MavenPublication) {
        from components.java
        groupId "com.linkedin.avro2tf"
        artifactId "${project.name}"

        artifact sourcesJar
        artifact javadocJar

        pom {
          name = "${project.name}".toString()
          description = "${project.name}".toString()
          url = 'https://github.com/linkedin/Avro2TF'
          licenses {
            license {
              name = "BSD 2-Clause"
              url = "https://opensource.org/licenses/BSD-2-Clause"
            }
          }
          developers {
            developer {
              id = 'zhangxuhong'
              name = 'Xuhong Zhang'
            }
            developer {
              id = 'cyzhangchenya'
              name = 'Chenya Zhang'
            }
          }
          scm {
            connection = 'https://github.com/linkedin/Avro2TF.git'
            developerConnection = 'git@github.com:linkedin/Avro2TF.git'
            url = 'https://github.com/linkedin/Avro2TF'
          }
        }
      }
    }
  }

  bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['avro2tfPublication']
    pkg {
      repo = 'maven'
      name = 'Avro2TF'
      userOrg = 'linkedin'
      licenses = ['BSD 2-CLAUSE']
      vcsUrl = 'https://github.com/linkedin/Avro2TF.git'
      labels = ['tensorflow', 'spark', 'avro']
      publicDownloadNumbers = true
      version {
        name = project.version
        released = new Date()
      }
    }
  }
}
